apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.jtakakura'
archivesBaseName = 'gradle-robovm-plugin'
version = '0.0.2'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
    roboVMVersion = "0.0.7"

    mavenVersion = '3.1.1'
    aetherVersion = '0.9.0.M3'
    wagonVersion = '1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "org.apache.maven:maven-aether-provider:${mavenVersion}"
    compile "org.apache.maven.wagon:wagon-ssh:${wagonVersion}"
    compile "org.eclipse.aether:aether-connector-basic:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-wagon:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-classpath:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-file:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-http:${aetherVersion}"
    compile "org.eclipse.aether:aether-util:${aetherVersion}"
    compile "org.eclipse.aether:aether-test-util:${aetherVersion}"
    compile "org.eclipse.aether:aether-api:${aetherVersion}"
    compile "org.eclipse.aether:aether-impl:${aetherVersion}"

    compile "org.robovm:robovm-compiler:${roboVMVersion}"
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required = { gradle.taskGraph.hasTask(uploadArchives) && !version.endsWith("SNAPSHOT") }
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}
